apply plugin: 'com.jfrog.bintray'

version = BINTRAY_LIB_VERSION

if (project.hasProperty("android")) {
    // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else {
    // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = project.hasProperty("BINTRAY_COMMON_USERNAME") ? BINTRAY_COMMON_USERNAME : ""
    key = project.hasProperty("BINTRAY_COMMON_APIKEY") ? BINTRAY_COMMON_APIKEY : ""

    configurations = ['archives']
    pkg {
        repo = project.hasProperty("BINTRAY_COMMON_REPO") ? BINTRAY_COMMON_REPO : ""
        name = BINTRAY_LIB_REPO_NAME
        desc = BINTRAY_LIB_DESCRIPTION
        websiteUrl = BINTRAY_LIB_SITE_URL
        vcsUrl = BINTRAY_LIB_GIT_URL
        licenses = [project.hasProperty("BINTRAY_COMMON_LICENSE_ID") ? BINTRAY_COMMON_LICENSE_ID : ""]
        publish = true
        publicDownloadNumbers = true
        version {
            desc = BINTRAY_LIB_DESCRIPTION
            gpg {
                sign = true
                passphrase = project.hasProperty("BINTRAY_COMMON_GPG_PASSWORD") ? BINTRAY_COMMON_GPG_PASSWORD : ""
            }
        }
    }
}